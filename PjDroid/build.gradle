buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'PjDroid'

    publishedGroupId = 'com.mahmudur85.lib'
    libraryName = 'PjDroid'
    artifact = 'PjDroid'

    libraryDescription = 'An Android Library for PJSIP PJSUA2 API.'

    siteUrl = 'https://github.com/mahmudur85/PjDroid'
    gitUrl = 'https://github.com/mahmudur85/PjDroid.git'

    libraryVersion = '2.0.0'

    developerId = 'mahmudur85'
    developerName = 'Khandker Mahmudur Rahman'
    developerEmail = 'mahmudur85@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

ext.versions = [
        'minSdk'        : 19,
        'compileSdk'    : 28,
        'targetSdk'     : 28,
        'buildTools'    : '28.0.3',

        'release'       : libraryVersion,
        'code'          : 4,
]

android {
    buildToolsVersion = '28.0.3'
    android {
        compileSdkVersion versions.compileSdk
        buildToolsVersion versions.buildTools

        defaultConfig {
            minSdkVersion versions.minSdk
            targetSdkVersion versions.targetSdk

            versionCode versions.code
            versionName versions.release
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
}

group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}
